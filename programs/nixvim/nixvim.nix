{ flakeNixpkgs, pkgs, ... }:

{
  programs.nixvim = {
    enable = true;
    defaultEditor = true;
    viAlias = true;
    vimAlias = true;
    enableMan = false; # Fix LSP package bug

    globals.mapleader = " ";
    diagnostic.settings.float.border = "rounded";
    extraConfigLua = builtins.readFile ./config.lua;

    nixpkgs = {
      source = flakeNixpkgs;
      config.allowUnfree = true;
    };

    opts = {
      number = true;
      relativenumber = true;
      linebreak = true;
      breakindent = true;
      clipboard = "unnamedplus";
      updatetime = 300;
      autoread = true;
      scrolloff = 10; # Leave 10 lines above/below cursor.
    };

    highlightOverride = {
      Todo = {
        fg = "#fabd2f";
        bg = "NONE";
        bold = true;
      };
    };

    plugins = {
      # Nerd font icons
      web-devicons.enable = true;

      # Language server protocol
      lsp = {
        enable = true;

        servers = {
          clojure_lsp.enable = true;
          eslint.enable = true;
          lua_ls.enable = true;
          nil_ls = {
            enable = true;
            settings.nix.flake.autoArchive = true; # Auto-fetch flake inputs
          };
          ruff.enable = true;
          tailwindcss = {
            enable = true;
            settings.tailwindCSS.colorDecorators = false;
          };
          terraformls.enable = true;
          texlab.enable = true;
          ts_ls.enable = true;
          ty.enable = true;
        };

        keymaps = {
          lspBuf = {
            gd = "definition";
          };
        };
      };

      # Syntax highlighting and code parsing
      treesitter = {
        enable = true;
        # TODO: Restore allGrammars once nixpkgs ships a diff parser matching its queries
        grammarPackages = builtins.filter (
          g: !(pkgs.lib.hasInfix "diff" g.name)
        ) pkgs.vimPlugins.nvim-treesitter.allGrammars;

        highlight.enable = true;
        indent.enable = true;
      };

      # Syntax tree navigation/selection
      flash = {
        enable = true;
        settings.modes.treesitter.enable = true;
      };

      # Formatter
      conform-nvim = {
        enable = true;

        settings = {
          format_on_save.lsp_fallback = true;

          formatters_by_ft = {
            arduino = [ "clang_format" ];
            c = [ "clang_format" ];
            cpp = [ "clang_format" ];
            css = [ "prettier" ];
            graphql = [ "prettier" ];
            handlebars = [ "prettier" ];
            html = [ "prettier" ];
            javascript = [
              "eslint_d"
              "prettier"
            ];
            javascriptreact = [
              "eslint_d"
              "prettier"
            ];
            json = [ "prettier" ];
            json5 = [ "prettier" ];
            jsonc = [ "prettier" ];
            less = [ "prettier" ];
            markdown = [ "prettier" ];
            "markdown.mdx" = [ "prettier" ];
            nix = [ "nixfmt" ];
            python = [
              "ruff_fix"
              "ruff_format"
            ];
            scss = [ "prettier" ];
            sh = [ "shfmt" ];
            terraform = [ "terraform_fmt" ];
            "terraform-vars" = [ "terraform_fmt" ];
            tex = [ "tex-fmt" ];
            typescript = [
              "eslint_d"
              "prettier"
            ];
            typescriptreact = [
              "eslint_d"
              "prettier"
            ];
            vue = [ "prettier" ];
            yaml = [ "prettier" ];
          };
        };
      };

      cmp = {
        enable = true;
        autoEnableSources = true;
        settings = {
          sources = [
            { name = "nvim_lsp"; }
            { name = "buffer"; }
          ];
          mapping = {
            "<cr>" = "cmp.mapping.confirm({ select = true })";
            "<Tab>" = "cmp.mapping.select_next_item()";
            "<S-Tab>" = "cmp.mapping.select_prev_item()";
          };
        };
      };

      telescope = {
        enable = true;
        extensions.fzf-native.enable = true;
      };

      # File explorer panel
      neo-tree = {
        enable = true;

        settings = {
          log_level = "warn";
          close_if_last_window = true;

          filesystem = {
            filtered_items = {
              visible = true;
              hide_dotfiles = false;
              hide_gitignored = false;
            };

            follow_current_file.enabled = true;
          };
        };
      };

      # Easy commenting/uncommenting
      comment.enable = true;

      # Git QoL in Neovim
      gitsigns = {
        enable = true;
        settings.current_line_blame = true;
      };

      neoscroll = {
        enable = true;
        settings.easing_function = "circular";
      };

      # Better navigation between Neovim and tmux
      tmux-navigator.enable = true;

      # Image rendering
      image = {
        enable = true;
        settings = {
          backend = "kitty";
          tmux_show_only_in_active_window = true;

          integrations.markdown = {
            only_render_image_at_cursor = true;
            only_render_image_at_cursor_mode = "inline";
          };
        };
      };

      # Markdown rendering
      render-markdown.enable = true;

      # Lua for Neovim
      lazydev.enable = true;

      # Python virtual environment selector
      venv-selector.enable = true;
    };

    colorschemes.gruvbox = {
      enable = true;

      settings = {
        contrast_dark = "dark";
        transparent_mode = true;
      };
    };

    keymaps = [
      {
        mode = "n";
        key = "<Esc>";
        action = "<cmd>nohlsearch<cr>";
        options.desc = "Clear search highlight";
      }
      {
        mode = "n";
        key = "<leader><cr>";
        action = "i<cr><Esc>";
        options.desc = "Insert line break";
      }

      # Move by display line (unless a count is given)
      {
        mode = [
          "n"
          "x"
        ];
        key = "j";
        action = "v:count == 0 ? 'gj' : 'j'";
        options = {
          expr = true;
          desc = "Down by display line";
        };
      }
      {
        mode = [
          "n"
          "x"
        ];
        key = "k";
        action = "v:count == 0 ? 'gk' : 'k'";
        options = {
          expr = true;
          desc = "Up by display line";
        };
      }
      {
        mode = [
          "n"
          "x"
        ];
        key = "<Down>";
        action = "v:count == 0 ? 'gj' : 'j'";
        options = {
          expr = true;
          desc = "Down by display line";
        };
      }
      {
        mode = [
          "n"
          "x"
        ];
        key = "<Up>";
        action = "v:count == 0 ? 'gk' : 'k'";
        options = {
          expr = true;
          desc = "Up by display line";
        };
      }
      {
        mode = "n";
        key = "<leader>ca";
        action = "<cmd>lua vim.lsp.buf.code_action()<cr>";
        options.desc = "Code actions";
      }

      # Ctrl + arrow split/pane navigation
      {
        mode = "n";
        key = "<C-Left>";
        action = "<cmd>TmuxNavigateLeft<cr>";
        options.desc = "Navigate to left split/pane";
      }
      {
        mode = "n";
        key = "<C-Down>";
        action = "<cmd>TmuxNavigateDown<cr>";
        options.desc = "Navigate to below split/pane";
      }
      {
        mode = "n";
        key = "<C-Up>";
        action = "<cmd>TmuxNavigateUp<cr>";
        options.desc = "Navigate to above split/pane";
      }
      {
        mode = "n";
        key = "<C-Right>";
        action = "<cmd>TmuxNavigateRight<cr>";
        options.desc = "Navigate to right split/pane";
      }

      # Neo-tree
      {
        mode = "n";
        key = "<leader>e";
        action = "<cmd>Neotree toggle<cr>";
        options.desc = "Toggle explorer";
      }

      # Telescope
      {
        mode = "n";
        key = "<leader>ff";
        action = "<cmd>Telescope find_files<cr>";
        options.desc = "Find files";
      }
      {
        mode = "n";
        key = "<leader>fg";
        action = "<cmd>Telescope live_grep<cr>";
        options.desc = "Live grep";
      }
      {
        mode = "n";
        key = "<leader>fb";
        action = "<cmd>Telescope buffers sort_mru=true<cr>";
        options.desc = "Open buffers";
      }
      {
        mode = "n";
        key = "<leader>fr";
        action = "<cmd>Telescope oldfiles<cr>";
        options.desc = "Recent files";
      }
      {
        mode = "n";
        key = "<leader>fc";
        action = "<cmd>Telescope git_status<cr>";
        options.desc = "Changed files (git)";
      }
      {
        mode = "n";
        key = "<leader>gl";
        action = "<cmd>Telescope git_commits<cr>";
        options.desc = "Git commits";
      }
      {
        mode = "n";
        key = "<leader>fs";
        action = "<cmd>Telescope lsp_document_symbols<cr>";
        options.desc = "LSP symbols";
      }
      {
        mode = "n";
        key = "gi";
        action = "<cmd>Telescope lsp_implementations<cr>";
        options.desc = "Implementations";
      }
      {
        mode = "n";
        key = "gr";
        action = "<cmd>Telescope lsp_references<cr>";
        options.desc = "References";
      }

      # Flash
      {
        mode = [
          "n"
          "x"
          "o"
        ];
        key = "s";
        action = "<cmd>lua require('flash').jump()<cr>";
        options.desc = "Flash";
      }
      {
        mode = [
          "n"
          "x"
          "o"
        ];
        key = "S";
        action = "<cmd>lua require('flash').treesitter()<cr>";
        options.desc = "Flash Treesitter";
      }
      {
        mode = [
          "o"
          "x"
        ];
        key = "R";
        action = "<cmd>lua require('flash').treesitter_search()<cr>";
        options.desc = "Treesitter search";
      }

      # Git
      {
        mode = "n";
        key = "<leader>gp";
        action = "<cmd>Gitsigns preview_hunk<cr>";
        options.desc = "Preview Git hunk";
      }
      {
        mode = "n";
        key = "<leader>gr";
        action = "<cmd>Gitsigns reset_hunk<cr>";
        options.desc = "Reset Git hunk";
      }

      # Virtual environment selector
      {
        mode = "n";
        key = "<leader>vs";
        action = "<cmd>VenvSelect<cr>";
        options.desc = "Select Python virtual environment";
      }
    ];
  };

  home.file.".editorconfig".source = ./.editorconfig;

  # Fixes for treesitter syntax highlighting
  xdg.configFile = {
    "nvim/queries/ecma/highlights.scm".text = ''
      [
        "break"
        "case"
        "catch"
        "continue"
        "default"
        "do"
        "else"
        "finally"
        "for"
        "if"
        "return"
        "switch"
        "throw"
        "try"
        "while"
        "const"
        "let"
        "var"
        "function"
        "class"
        "new"
        "async"
        "await"
        "import"
        "export"
        "from"
        "as"
      ] @keyword

      (string) @string
      (number) @number
      (true) @boolean
      (false) @boolean
      (null) @constant.builtin
      (comment) @comment
    '';

    "nvim/queries/jsx/highlights.scm".text = ''
      (jsx_element
        open_tag: (jsx_opening_element
          name: (identifier) @tag))

      (jsx_element
        close_tag: (jsx_closing_element
          name: (identifier) @tag))

      (jsx_self_closing_element
        name: (identifier) @tag)

      (jsx_attribute
        (property_identifier) @tag.attribute)
    '';
  };
}
